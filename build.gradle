plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
}

version = "1.0.0-beta.5+1.20.4"
group = "dev.falseresync"

base {
    archivesName = "explorers-delight"
}

repositories {
    mavenCentral()
    // Trinkets, ModMenu & REI dependencies
    maven { url = "https://maven.terraformersmc.com/" }
    // Trinkets dependencies
    maven { url = 'https://maven.ladysnake.org/releases' }
    // YetAnotherConfigLib
    maven { url = 'https://maven.isxander.dev/releases' }
    // REI & it's slew of dependencies
    maven { url = "https://maven.shedaniel.me/" }
    // Common Protection API
    maven {
        url = "https://maven.nucleoid.xyz/"
        content { includeGroup "eu.pb4" }
    }
    // MixinTrace
    maven {
        url = "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }
}

loom {
    accessWidenerPath = file("src/main/resources/exdel.accesswidener")
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=exdel"

            runDir "build/datagen"
        }
    }
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:1.20.4"
    mappings "net.fabricmc:yarn:1.20.4+build.3:v2"
    modImplementation "net.fabricmc:fabric-loader:0.15.2"

    modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.3+1.20.4"
    modImplementation 'dev.isxander.yacl:yet-another-config-lib-fabric:3.3.1+1.20.4'
    modImplementation include("eu.pb4:common-protection-api:1.0.0")

    // Trinkets
    // CCA changes their Maven regularly so the deps don't get properly pulled :p
    def cca_version = "5.3.0"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
    modImplementation "dev.emi:trinkets:3.8.0"

    // Compat
    def modmenu_version = '9.0.0-pre.1'
    modApi "com.terraformersmc:modmenu:${modmenu_version}"
    modLocalRuntime "com.terraformersmc:modmenu:${modmenu_version}"

    // Convenience
    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-fabric:14.0.680"
    modLocalRuntime "maven.modrinth:mixintrace:1.1.1+1.17"
//    modLocalRuntime "maven.modrinth:goml-reserved:`1.11.0`+1.20.3-rc1"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
