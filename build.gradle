plugins {
    id 'fabric-loom' version '1.4-SNAPSHOT'
}

version = "1.0.0-beta.4+1.20.2"
group = "dev.falseresync"

base {
    archivesName = "explorers-delight"
}

repositories {
    // Trinkets & REI
    maven { url = "https://maven.terraformersmc.com/" }
    // Trinkets
    maven { url = 'https://maven.ladysnake.org/releases' }
    // Common Protection API
    maven { url = "https://maven.nucleoid.xyz/" }
    // Omega Config
    maven { url = "https://maven.draylar.dev/releases" }
    // Flan
    maven {
        url = 'https://gitlab.com/api/v4/projects/21830712/packages/maven'
        content { includeGroup "io.github.flemmli97" }
    }
    // GOML
    maven {
        url = "https://api.modrinth.com/maven"
        content { includeGroup "maven.modrinth" }
    }
    maven {
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
        content { includeGroup 'com.jamieswhiteshirt' }
    }
    // REI & it's slew of dependencies
    maven { url = "https://maven.shedaniel.me/" }

}

loom {
    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagen"
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=explorers-delight"

            runDir "build/datagen"
        }
    }
}

// Add the generated resources to the main source set
sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:1.20.2"
    mappings "net.fabricmc:yarn:1.20.2+build.4:v2"
    modImplementation "net.fabricmc:fabric-loader:0.15.0"

    modImplementation "net.fabricmc.fabric-api:fabric-api:0.91.1+1.20.2"
    modImplementation include("eu.pb4:common-protection-api:1.0.0")
//    modImplementation include("dev.draylar.omega-config:omega-config-base:1.4.0+1.20.1")

    // CCA changes their Maven regularly so the deps don't get properly pulled :p
    def cca_version = "5.3.0"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:${cca_version}"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${cca_version}"
    modImplementation "dev.emi:trinkets:3.8.0"

    // Compatibility testing
//    modRuntimeOnly("io.github.flemmli97:flan:1.20.1-1.8.10-fabric") {
//        transitive = false // Remove this if you want to have all those optional dependencies
//    }
//    modRuntimeOnly "maven.modrinth:goml-reserved:1.10.2+1.20.2"

    // Convenience
//    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:13.0.678"
    modRuntimeOnly "com.terraformersmc:modmenu:8.0.0"
    modRuntimeOnly "maven.modrinth:mixintrace:1.1.1+1.17"
}

loom {
//    accessWidenerPath = file("src/main/resources/exdel.accesswidener")
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.base.archivesName.get()}" }
    }
}
