plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	accessWidenerPath = file("src/main/resources/exdel.accesswidener")
}

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
		name 'Gegy'
		url 'https://maven.gegy.dev'
	}
	maven {
		url 'https://jitpack.io'
	}
	maven {
		url "https://maven.shedaniel.me/"
	}
	// All of these are for Flan :P
	maven {
		name = "Flemmli97"
		url "https://gitlab.com/api/v4/projects/21830712/packages/maven"
	}
	maven {
		name = "Gunpowder"
		url = "https://maven.martmists.com/releases"
	}
	maven {
		name = "HavenKing"
		url = "https://hephaestus.dev/release"
	}
	maven {
		name = "Permissions"
		url = "https://maven.nucleoid.xyz"
	}
	// For GOML
	maven {
		url = "https://maven.jamieswhiteshirt.com/libs-release/"
		content {
			includeGroup 'com.jamieswhiteshirt'
		}
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Hard dependencies
	modImplementation "dev.emi:trinkets:${project.trinkets_version}"
	include "dev.emi:trinkets:${project.trinkets_version}"

	modImplementation "com.github.Draylar:omega-config:${project.omega_version}"
	include "com.github.Draylar:omega-config:${project.omega_version}"

	// Soft (compatibility) dependencies
	modCompileOnly "com.github.Draylar:get-off-my-lawn:${project.goml_version}"
	modRuntimeOnly "com.github.Draylar:get-off-my-lawn:${project.goml_version}"

	modCompileOnly ("io.github.flemmli97:flan:1.17.1-${project.flan_version}") {
		transitive = false
	}
//	modRuntimeOnly ("io.github.flemmli97:flan:1.17.1-${project.flan_version}") {
//		transitive = false
//	}

	// Other mods
	modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"
	modRuntimeOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// ensure that the encoding is set to UTF-8, no matter what the system default is
	// this fixes some edge cases with special characters not displaying correctly
	// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	// If Javadoc is generated, this must be specified in that task too.
	it.options.encoding = "UTF-8"

	// Minecraft 1.17 (21w19a) upwards uses Java 16.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}
